from list import partial
from nat import Nat
from rat import Rat
from monoid import Monoid
from real.real_seq import cauchy_bound, tail_bound, neg_rat_seq, eventual_eq
from real.real_ring import lift_seq, converges, converges_to, limit, eventual_lb, limit_rat, rat_seq, mul_rat_seq
from real.real_series import Real, neg_seq
numerals Real

// This file defines real division and proves theorems about it.

define recip_rat_seq(a: Nat -> Rat, n: Nat) -> Rat {
    a(n).reciprocal
}

theorem neg_recip_rat_seq(a: Nat -> Rat) {
    neg_rat_seq(recip_rat_seq(a)) = recip_rat_seq(neg_rat_seq(a))
}

// Division works for positive reals
theorem recip_rat_seq_pos_converges(a: Nat -> Rat, b: Real) {
    converges_to(lift_seq(a), b) and b.is_positive
    implies converges(lift_seq(recip_rat_seq(a)))
} by {
    let b_lb: Rat satisfy {
        Rat.0 < b_lb and Real.from_rat(b_lb) < b
    }
    let lsa = lift_seq(a)
    eventual_lb(lsa, Real.from_rat(b_lb))
    let n1: Nat satisfy {
        forall(i: Nat) {
            n1 < i implies Real.from_rat(b_lb) <= lsa(i)
        }
    }
    forall(i: Nat) {
        if n1 < i {
            b_lb <= a(i)
        }
    }
    
    forall(eps: Real) {
        if eps.is_positive {
            let reps: Rat satisfy {
                reps.is_positive and Real.from_rat(reps) < eps
            }

            // We had to work backwards to find this condition
            (b_lb * b_lb).is_positive
            (reps * (b_lb * b_lb)).is_positive
            let eps2: Rat satisfy {
                eps2.is_positive and eps2 < reps * (b_lb * b_lb)
            }
            eps2 / (b_lb * b_lb) < reps
            let eps3: Rat satisfy {
                eps3 + eps3 = eps2
            }
            eps3.is_positive
            
            // Find where the original sequence is within eps_bound of b
            let n2: Nat satisfy {
                tail_bound(lift_seq(a), b, n2, Real.from_rat(eps3))
            }
            forall(i: Nat) {
                if n2 <= i {
                    n2 <= Nat.0 + i
                    n2 <= Nat.0 + (i + Nat.0)
                    tail_bound(lift_seq(a), Real.new(b.gt_rat), n2, Real.from_rat(eps3))
                    lift_seq(a)(i).is_close(b, Real.from_rat(eps3))
                }
            }
            
            // Choose a bound above both
            let n: Nat satisfy {
                n1 < n and n2 < n
            }
            
            // Now we show that the reciprocal sequence satisfies the Cauchy property
            forall(i: Nat, j: Nat) {
                if n <= i and n <= j {
                    // Both terms are close to b
                    n2 <= i
                    lift_seq(a)(i).is_close(b, Real.from_rat(eps3))
                    n2 <= j
                    lift_seq(a)(j).is_close(b, Real.from_rat(eps3))
                    
                    // So their diff is small
                    lift_seq(a, i).is_close(lift_seq(a, j), Real.from_rat(eps3) + Real.from_rat(eps3))
                    a(i).is_close(a(j), eps2)
                    let diff = a(j) - a(i)
                    diff.abs < eps2

                    // Both terms are non-zero
                    n1 <= i
                    b_lb <= a(i)
                    a(i) != Rat.0
                    n1 <= j
                    b_lb <= a(j)
                    a(j) != Rat.0

                    // So their product is large
                    let prod = a(i) * a(j)
                    b_lb * b_lb <= prod
                    b_lb * b_lb <= prod.abs
                    (b_lb * b_lb).is_positive
                    prod.abs.is_positive

                    diff.abs * (b_lb * b_lb) < eps2 * prod.abs
                    diff.abs < eps2 * prod.abs / (b_lb * b_lb)
                    eps2 * prod.abs / (b_lb * b_lb) = (eps2 / (b_lb * b_lb)) * prod.abs 
                    diff.abs < (eps2 / (b_lb * b_lb)) * prod.abs 
                    diff.abs / prod.abs < eps2 / (b_lb * b_lb)
                    diff.abs / prod.abs = (diff / prod).abs
                    (diff / prod).abs < eps2 / (b_lb * b_lb)
                    (diff / prod).abs < reps
                    ((a(j) - a(i)) / (a(i) * a(j))).abs < reps
                    a(i).reciprocal - a(j).reciprocal = ((a(j) - a(i)) / (a(i) * a(j)))
                    (a(i).reciprocal - a(j).reciprocal).abs < reps
                    a(i).reciprocal.is_close(a(j).reciprocal, reps)
                    recip_rat_seq(a)(i).is_close(recip_rat_seq(a)(j), reps)
                    lift_seq(recip_rat_seq(a))(i).is_close(lift_seq(recip_rat_seq(a))(j), Real.from_rat(reps))
                    lift_seq(recip_rat_seq(a))(i).is_close(lift_seq(recip_rat_seq(a))(j), eps)
                }
            }
            
            // This establishes the Cauchy criterion
            cauchy_bound(lift_seq(recip_rat_seq(a)), n, eps)
        }
    }
    
    // Therefore the sequence converges
    converges(lift_seq(recip_rat_seq(a)))
}

// Division works for all nonzero reals
theorem recip_rat_seq_converges(a: Nat -> Rat, b: Real) {
    converges_to(lift_seq(a), b) and b != Real.0
    implies converges(lift_seq(recip_rat_seq(a)))
} by {
    if b.is_positive {
        // This is the previous theorem
        converges(lift_seq(recip_rat_seq(a)))
    } else {
        b.is_negative
        (-b).is_positive
        limit(lift_seq(a)) = b
        converges_to(neg_seq(lift_seq(a)), -b)
        converges_to(lift_seq(neg_rat_seq(a)), -b)
        converges(lift_seq(recip_rat_seq(neg_rat_seq(a))))
        recip_rat_seq(neg_rat_seq(a)) = neg_rat_seq(recip_rat_seq(a))
        converges(lift_seq(neg_rat_seq(recip_rat_seq(a))))
        lift_seq(neg_rat_seq(recip_rat_seq(a))) = neg_seq(lift_seq(recip_rat_seq(a)))
        converges(neg_seq(lift_seq(recip_rat_seq(a))))
        converges(lift_seq(recip_rat_seq(a)))
    }
}

attributes Real {
    /// The reciprocal of this real number (`1/x`). For zero, returns zero.
    define reciprocal(self) -> Real {
        if self = Real.0 {
            Real.0
        } else {
            limit_rat(recip_rat_seq(rat_seq(self)))
        }
    }
}

define eventually_nonzero(a: Nat -> Real) -> Bool {
    exists(n: Nat) {
        forall(i: Nat) {
            n <= i implies a(i) != Real.0
        }
    }
}

theorem pos_imp_eventually_nonzero(a: Nat -> Real) {
    converges(a) and limit(a).is_positive
    implies eventually_nonzero(a)
} by {
    let n: Nat satisfy {
        forall(i: Nat) {
            n <= i implies a(i).is_close(limit(a), limit(a))
        }
    }
    forall(i: Nat) {
        if n <= i {
            a(i).is_close(limit(a), limit(a))
            a(i) != Real.0
        }
    }
}

theorem nonzero_imp_eventually_nonzero(a: Nat -> Real) {
    converges(a) and limit(a) != Real.0
    implies eventually_nonzero(a)
} by {
    if limit(a).is_positive {
        eventually_nonzero(a)
    } else {
        limit(a).is_negative
        limit(neg_seq(a)) = -limit(a)
        limit(neg_seq(a)).is_positive
        let n: Nat satisfy {
            forall(i: Nat) {
                n <= i implies neg_seq(a)(i) != Real.0
            }
        }
        forall(i: Nat) {
            if n <= i {
                neg_seq(a)(i) != Real.0
                a(i) != Real.0
            }
        }
        eventually_nonzero(a)
    }
}

theorem mul_reciprocal(a: Real) {
    a != Real.0 implies a * a.reciprocal = Real.1
} by {
    a = limit_rat(rat_seq(a))
    a.reciprocal = limit_rat(recip_rat_seq(rat_seq(a)))
    a * a.reciprocal = limit_rat(rat_seq(a)) * limit_rat(recip_rat_seq(rat_seq(a)))
    limit(lift_seq(rat_seq(a))) = a
    limit(lift_seq(rat_seq(a))) != Real.0
    let n: Nat satisfy {
        forall(i: Nat) {
            n <= i implies lift_seq(rat_seq(a))(i) != Real.0
        }
    }

    // We want to show that the product of the sequences is eventually
    // just a constant one.
    let prod = mul_rat_seq(rat_seq(a), recip_rat_seq(rat_seq(a)))

    forall(i: Nat) {
        if n <= i {
            rat_seq(a, i) != Rat.0
            rat_seq(a, i) * recip_rat_seq(rat_seq(a), i) = Rat.1
            prod(i) = Rat.1
            lift_seq(prod)(i) = Real.1
        }
    }
    eventual_eq(lift_seq(prod), Real.1)

    limit_rat(rat_seq(a)) * limit_rat(recip_rat_seq(rat_seq(a))) = limit_rat(mul_rat_seq(rat_seq(a), recip_rat_seq(rat_seq(a))))
    a * a.reciprocal = limit_rat(mul_rat_seq(rat_seq(a), recip_rat_seq(rat_seq(a))))
    a * a.reciprocal = limit_rat(prod)
}

theorem zero_reciprocal {
    Real.0.reciprocal = Real.0
}

from field import Field

theorem zero_is_different_than_one {
    Real.0 != Real.1
}

instance Real: Field {
    let inverse = Real.reciprocal
}

attributes Real {
    /// The quotient of two real numbers (`self/other`).
    define div(self, other: Real) -> Real {
        self * other.reciprocal
    }
}

theorem mul_left_cancel(a: Real, b: Real, c: Real) {
    a = b * c and b != Real.0 implies a / b = c
}

theorem geom_series(r: Real) {
    r.abs < 1 implies
    limit(partial(r.pow)) = 1 / (1 - r)
} by {
    1 + r * limit(partial(r.pow)) = limit(partial(r.pow))
    1 = limit(partial(r.pow)) - r * limit(partial(r.pow))
    1 = (1 - r) * limit(partial(r.pow))
    (1 - r) != Real.0
    limit(partial(r.pow)) = 1 / (1 - r)
}
