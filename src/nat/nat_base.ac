from order import PartialOrder, LinearOrder
from util import is_reflexive, is_transitive, is_antisymmetric

/// Natural numbers, the soul of arithmetic.
/// We build natural numbers from Acorn's inherent properties of inductive types.
inductive Nat {
    /// Zero is a natural number, because it's much more convenient this way.
    0

    /// The successor of a natural number is also a natural number.
    suc(Nat)
}

attributes Nat {
    let 1: Nat = Nat.0.suc
    let 2: Nat = Nat.1.suc
    let 3: Nat = Nat.2.suc
    let 4: Nat = Nat.3.suc
    let 5: Nat = Nat.4.suc
    let 6: Nat = Nat.5.suc
    let 7: Nat = Nat.6.suc
    let 8: Nat = Nat.7.suc
    let 9: Nat = Nat.8.suc
    let 10: Nat = Nat.9.suc

    /// Addition is defined recursively.
    define add(self, other: Nat) -> Nat {
        match other {
            Nat.0 {
                self
            }
            Nat.suc(pred) {
                self.add(pred).suc
            }
        }
    }
}

numerals Nat


// Now let's have some theorems.

theorem add_zero_right(a: Nat) { a + 0 = a }

theorem one_plus_one { 1 + 1 = 2  } by {
    1 + 0.suc = 2
}

theorem add_zero_left(a: Nat) { 0 + a = a } by {
    0 + 0 = 0
    add_zero_left(0)
    forall(x: Nat) {
        if add_zero_left(x) {
            0 + x = x
            0 + x.suc = x.suc
            add_zero_left(x.suc)
        }
    }
    add_zero_left(a)
}

theorem add_suc_right(a: Nat, b: Nat) {
    a + b.suc = (a + b).suc
}

theorem add_suc_left(a: Nat, b: Nat) { a.suc + b = (a + b).suc } by {
    define f(x: Nat) -> Bool { add_suc_left(a, x) }
    a.suc + 0 = (a + 0).suc
    add_suc_left(a, 0)
    forall(x: Nat) {
        if f(x) {
            add_suc_left(a, x)
            a.suc + x.suc = (a + x.suc).suc
            add_suc_left(a, x.suc)
            f(x.suc)
        }
    }
    add_suc_left(a, b)
}

theorem add_one_right(a: Nat) {
    a + 1 = a.suc
} by {
    a + 0.suc = a.suc
}

theorem add_one_left(a: Nat) {
    1 + a = a.suc
} by {
    0.suc + a = a.suc
}

theorem suc_ne(a: Nat) { a.suc != a } by {
    suc_ne(0)
    forall(x: Nat) {
        if suc_ne(x) {
            x.suc != x
            x.suc.suc != x.suc
            suc_ne(x.suc)
        }
    }
    suc_ne(a)
}

theorem suc_suc_ne(a: Nat) { a.suc.suc != a } by {
    suc_suc_ne(0)
    forall(x: Nat) {
        if suc_suc_ne(x) {
            x.suc.suc != x
            x.suc.suc.suc != x.suc
            suc_suc_ne(x.suc)
        }
    }
    suc_suc_ne(a)
}

theorem add_comm(a: Nat, b: Nat) { a + b = b + a } by {
    define f(x: Nat) -> Bool { add_comm(x, b) }
    b + 0 = 0 + b
    add_comm(0, b)
    forall(x: Nat) {
        if f(x) {
            add_comm(x, b)
            b + x.suc = (x + b).suc
            b + x.suc = x.suc + b
            add_comm(x.suc, b)
            f(x.suc)
        }
    }
    add_comm(a, b)
}

theorem add_assoc(a: Nat, b: Nat, c: Nat) { a + b + c = a + (b + c) } by {
    define f(x: Nat) -> Bool { add_assoc(x, b, c) }
    forall(x: Nat) {
        if f(x) {
            x.suc + b + c = (x + b + c).suc
            add_assoc(x, b, c)
            x.suc + b + c = x.suc + (b + c)
            add_assoc(x.suc, b, c)
            f(x.suc)
        }
    }
    0 + b + c = 0 + (b + c)
    add_assoc(0, b, c)
    add_assoc(a, b, c)
}

attributes Nat {
    /// Multiplication is defined recursively.
    define mul(self, b: Nat) -> Nat {
        match b {
            Nat.0 {
                0
            }
            Nat.suc(pred) {
                self.mul(pred) + self
            }
        }
    }

    /// The number formed by appending a digit to this one in base 10.
    define read(self, other: Nat) -> Nat {
        10 * self + other
    }
}

theorem mul_zero_right(a: Nat) { a * 0 = 0 }

theorem mul_zero_left(a: Nat) { 0 * a = 0 } by {
    mul_zero_left(0)
    forall(x: Nat) {
        if mul_zero_left(x) {
            0 + 0 * x = 0
            0 * x.suc = 0
            mul_zero_left(x.suc)
        }
    }
    mul_zero_left(a)
}

theorem mul_suc_right(a: Nat, b: Nat) {
    a * b.suc = a + a * b
}

theorem mul_suc_left(a: Nat, b: Nat) { a.suc * b = b + a * b } by {
    define f(x: Nat) -> Bool { mul_suc_left(a, x) }
    Nat.induction(f)
    0 + a * 0 = 0
    0 + a * 0 = a.suc * 0
    mul_suc_left(a, 0)
    forall(x: Nat) {
        if f(x) {
            mul_suc_left(a, x)
            a.suc * x = x + a * x
            a.suc * x.suc = a.suc + a.suc * x
            a.suc * x.suc = a.suc + (x + a * x)
            a * x.suc = a + a * x
            a.suc + (x + a * x) = a.suc + x + a * x
            a.suc + (x + a * x) = (a + x).suc + a * x
            a.suc + (x + a * x) = (x + a).suc + a * x
            a.suc + (x + a * x) = x.suc + a + a * x
            add_assoc(x.suc, a, a * x)
            x.suc + a + a * x = x.suc + (a + a * x)
            a.suc + (x + a * x) = x.suc + (a + a * x)
            a.suc * x.suc = x.suc + (a + a * x)
            a.suc * x.suc = x.suc + a * x.suc
            mul_suc_left(a, x.suc)
            f(x.suc)
        }
    }
    f(b)
}

theorem mul_one_one { 1 * 1 = 1 } by {
    1 * 1 = 1 * 0.suc
    1 * 1 = 1 + 1 * 0
    1 * 1 = 1 + 0
    1 * 1 = 1
}

theorem mul_comm(a: Nat, b: Nat) { a * b = b * a } by {
    define f(x: Nat) -> Bool { mul_comm(x, b) }
    b * 0 = 0 * b
    mul_comm(0, b)
    forall(x: Nat) {
        if f(x) {
            mul_comm(x, b)
            x * b = b * x
            x.suc * b = b + x * b
            b * x.suc = b + x * b
            x.suc * b = b * x.suc
            mul_comm(x.suc, b)
            f(x.suc)
        }
    }
    f(a)
}

theorem add_comm_4(a: Nat, b: Nat, c: Nat, d: Nat) {
    (a + b) + (c + d) = (a + c) + (b + d)
} by {
    a + (b + c) = a + c + b
}

theorem distrib_left(a: Nat, b: Nat, c: Nat) {
    a * (b + c) = a * b + a * c
} by {
    define f(x: Nat) -> Bool { distrib_left(x, b, c) }
    0 * b + 0 = 0
    0 * b + 0 * c = 0 * (b + c)
    distrib_left(0, b, c)
    forall(x: Nat) {
        if f(x) {
            distrib_left(x, b, c)
            x * (b + c) = x * b + x * c
            x.suc * (b + c) = b + c + x * (b + c)
            x.suc * (b + c) = b + c + (x * b + x * c)
            x.suc * (b + c) = (b + x * b) + (c + x * c)
            b + x * b = x.suc * b
            c + x * c = x.suc * c
            (b + x * b) + (c + x * c) = x.suc * b + (c + x * c)
            (b + x * b) + (c + x * c) = x.suc * b + x.suc * c
            x.suc * (b + c) = x.suc * b + x.suc * c
            distrib_left(x.suc, b, c)
            f(x.suc)
        }
    }
    distrib_left(a, b, c)
}

theorem distrib_right(a: Nat, b: Nat, c: Nat) {
    (a + b) * c = a * c + b * c
} by {
    c * a + b * c = c * (a + b)
}

theorem mul_assoc(a: Nat, b: Nat, c: Nat) { a * b * c = a * (b * c) } by {
    define f(x: Nat) -> Bool { mul_assoc(x, b, c) }
    0 * b * c = 0 * (b * c)
    mul_assoc(0, b, c)
    forall(x: Nat) {
        if f(x) {
            mul_assoc(x, b, c)
            x * b * c = x * (b * c)
            x.suc * b * c = (b + x * b) * c
            (b + x * b) * c = b * c + x * b * c
            x.suc * b * c = b * c + x * b * c
            x.suc * b * c = b * c + x * (b * c)
            x.suc * b * c = x.suc * (b * c)
            mul_assoc(x.suc, b, c)
            f(x.suc)
        }
    }
    mul_assoc(a, b, c)
}

theorem mul_one_right(a: Nat) { a * 1 = a } by {
    a + a * 0 = a
}

theorem mul_one_left(a: Nat) { 1 * a = a }

theorem add_cancels_left(a: Nat, b: Nat, c: Nat) { a + b = a + c implies b = c } by {
    define f(x: Nat) -> Bool { add_cancels_left(x, b, c) }
    if not add_cancels_left(0, b, c) {
        0 + c = 0 + b
        not add_cancels_left(0, b, 0 + c)
        false
    }
    forall(x: Nat) {
        if f(x) {
            if x.suc + b = x.suc + c {
                (x + c).suc = x.suc + b
                (x + b).suc = (x + c).suc
                x + b = x + c
                add_cancels_left(x, b, c)
                b = c
            }
            x.suc + b = x.suc + c implies b = c
            if not add_cancels_left(x.suc, b, c) {
                x.suc + c = x.suc + b
                c != b
                false
            }
            f(x.suc)
        }
    }
    f(a)
}

// Ordering

attributes Nat {
    /// `a <= b` if there's a natural number that can be added to `a` to get `b`.
    define lte(self, b: Nat) -> Bool {
        exists(c: Nat) {
            self + c = b
        }
    }
}

theorem lte_ref(a: Nat) {
    a <= a
}

theorem nat_is_reflexive {
    is_reflexive(Nat.lte)
}

theorem lte_trans(a: Nat, b: Nat, c: Nat) { a <= b and b <= c implies a <= c } by {
    define f(z: Nat) -> Bool {
        forall(x: Nat, y: Nat) { lte_trans(x, y, z) }
    }
    Nat.induction(f)
    forall(x: Nat, y: Nat) {
        if x <= y and y <= 0 {
            y = 0
            x <= 0
        }
        lte_trans(x, y, 0)
    }
    f(0)
    forall(z: Nat) {
        if f(z) {
            forall(x: Nat, y: Nat) {
                if x <= y and y <= z.suc {
                    let (d: Nat) satisfy { x + d = y }
                    let (e: Nat) satisfy { y + e = z.suc }
                    x + (d + e) = z.suc
                    x <= z.suc
                }
                lte_trans(x, y, z.suc)
            }
            f(z.suc)
        }
    }
    f(c)
}

theorem nat_is_transitive {
    is_transitive(Nat.lte)
}

theorem add_to_zero(a: Nat, b: Nat) { a + b = 0 implies a = 0 and b = 0 } by {
    define f(x: Nat) -> Bool { add_to_zero(x, b) }
    if not add_to_zero(0, b) {
        0 + b = 0
        b != 0
        false
    }
    forall(x: Nat) {
        x.suc + b != 0
        add_to_zero(x.suc, b)
        f(x.suc)
    }
    f(a)
    add_to_zero(a, b)
}

theorem lte_antisymm(a: Nat, b: Nat) {
    a <= b and b <= a implies a = b
} by {
    let c: Nat satisfy {
        a + c = b
    }
    let d: Nat satisfy {
        b + d = a
    }
    a + c + d = a
    a + (c + d) = a
    c + d = 0
    c = 0
    d = 0
    a = b
}

theorem nat_is_antisymmetric {
    is_antisymmetric(Nat.lte)
}

instance Nat: PartialOrder {
    let lte = Nat.lte
}

theorem lt_not_ref(a: Nat) {
    not (a < a)
}

theorem only_zero_lte_zero(a: Nat) {
    a <= 0 implies a = 0
} by {
    exists(k0: Nat) { a + k0 = 0 }
}

theorem not_lt_zero(a: Nat) {
    not a < 0
} by {
    if a < 0 {
        a != 0
        false
    }
}

theorem zero_or_suc(a: Nat) {
    a = 0 or exists(b: Nat) { a = b.suc }
}

theorem lte_cancel_suc(a: Nat, b: Nat) { a.suc <= b.suc implies a <= b } by {
    let c: Nat satisfy { a.suc + c = b.suc }
    a.suc + c = b.suc
    (a + c).suc = b.suc
    a + c = b
    a <= b
}

theorem lt_cancel_suc(a: Nat, b: Nat) {
    a.suc < b.suc implies a < b
} by {
    a.suc <= b.suc
    b.suc != a.suc
    a <= b and b != a
}

theorem lt_not_symm(a: Nat, b: Nat) { a < b implies not b < a } by {
    define f(x: Nat) -> Bool {
        forall(y: Nat) { lt_not_symm(x, y) }
    }
    f(0)
    forall(x: Nat) {
        if f(x) {
            forall(y: Nat) {
                if x.suc < y {
                    y != 0
                    zero_or_suc(y)
                    y = 0 or exists(z: Nat) { y = z.suc }
                    let (z: Nat) satisfy { y = z.suc }
                    x.suc < z.suc
                    x < z
                    lt_not_symm(x, z)
                    not z < x
                    not (z.suc < x.suc)
                    not (y < x.suc)
                }
                if not lt_not_symm(x.suc, y) {
                    x.suc < y
                    false
                }
                lt_not_symm(x.suc, y)
            }
            f(x.suc)
        }
    }
    f(a)
    lt_not_symm(a, b)
}

theorem lt_diff(a: Nat, b: Nat) {
    a < b implies exists(c: Nat) { a + c = b and c != 0 }
} by {
    a <= b
    let (c: Nat) satisfy { a + c = b }
    b != a
    c != 0
}

theorem lt_and_lte(a: Nat, b: Nat, c: Nat) { a < b and b <= c implies a < c } by {
    a <= b
    a <= c
    if a = c {
        c < b
        not b < a
        b != a
        false
    }
    a < c
}

theorem lte_and_lt(a: Nat, b: Nat, c: Nat) { a <= b and b < c implies a < c } by {
    b <= c
    a <= c
    if a = c {
        b < a
        false
    }
    a < c
}

theorem lt_trans(a: Nat, b: Nat, c: Nat) { a < b and b < c implies a < c } by {
    a <= b
}

theorem add_cancels_right(a: Nat, b: Nat, c: Nat) { b + a = c + a implies b = c } by {
    a + b = a + c
    b = c
}

theorem add_identity_right(a: Nat, b: Nat) {
    a + b = a implies b = 0
} by {
    exists(k0: Nat) { k0 + b = k0 + 0 }
}

theorem lt_add_suc(a: Nat, b: Nat) { a < a + b.suc } by {
    a <= a + b.suc
    if a = a + b.suc {
        a + 0 = a + b.suc
        0 = b.suc
        false
    }
}

theorem lt_suc(a: Nat) { a < a.suc } by {
    a.suc = a + 1
    a.suc = a + 0.suc
}

theorem lt_suc_left(a: Nat, b: Nat) { a < b implies a.suc = b or a.suc < b } by {
    let (c: Nat) satisfy { a + c = b and c != 0 }
    let (d: Nat) satisfy { d.suc = c }
    if d = 0 {
        a + 1 = b
        a.suc = b
    } else {
        (a + d).suc = b
        a.suc + d = b
        a.suc != b
        a.suc <= b
        a.suc < b
    }
}

theorem lt_suc_right(a: Nat, b: Nat) { a < b.suc implies a = b or a < b } by {
    if a != b {
        b.suc != a.suc
        a.suc < b.suc
        a < b
    }
}

theorem lt_add_left(a: Nat, b: Nat, c: Nat) { b < c implies a + b < a + c } by {
    let (d: Nat) satisfy { b + d = c }
    c != b
    d != 0
    a + b + d = a + c
    a + b < a + b + d
    a + b < a + c
}

theorem trichotomy(a: Nat, b: Nat) { a < b or b < a or a = b } by {
    define f(x: Nat) -> Bool { trichotomy(a, x) }
    if not trichotomy(a, 0) {
        a != 0
        not 0 < a
        0 + a != 0
        false
    }
    trichotomy(a, 0)
    f(0)
    forall(x: Nat) {
        if f(x) {
            trichotomy(a, x)
            a < x or x < a or a = x
            if a < x {
                x <= x.suc
                a < x.suc
                trichotomy(a, x.suc)
                f(x.suc)
            }
            if x < a {
                x.suc = a or x.suc < a
                if x.suc = a {
                    f(x.suc)
                } else {
                    trichotomy(a, x.suc)
                    f(x.suc)
                }
            }
            if a = x {
                a < x.suc
                trichotomy(a, x.suc)
                f(x.suc)
            }
            f(x.suc)
        }
    }
    trichotomy(a, b)
}

theorem lt_or_lte(a: Nat, b: Nat) { a < b or b <= a } by {
    if not (a < b) and not (b <= a) {
        not b < a
        b = a
        false
    }
}

theorem lt_imp_lte_suc(a: Nat, b: Nat) { a < b implies a.suc <= b } by {
    if not (a.suc <= b) {
        not a.suc < b
        a.suc = b
        false
    }
}

theorem lte_imp_not_lt(a: Nat, b: Nat) { a <= b implies not (b < a) } by {
    if a = b {
        not (b < a)
    } else {
        a < b
        not (b < a)
    }
}

theorem division_theorem(m: Nat, n: Nat) {
    0 < n implies exists(q: Nat, r: Nat) {
        r < n and m = q * n + r
    }
} by {
    define f(x: Nat) -> Bool { division_theorem(x, n) }
    if 0 < n {
        0 = 0 * n + 0
    }
    division_theorem(0, n)
    f(0)
    forall(x: Nat) {
        if f(x) {
            division_theorem(x, n)
            let (q: Nat, r: Nat) satisfy {
                r < n and x = q * n + r
            }
            if r.suc = n {
                x.suc = (q * n + r).suc
                x.suc = q * n + r.suc
                x.suc = q * n + n
                n + q * n = x.suc
                x.suc = q.suc * n
                x.suc = q.suc * n + 0
                division_theorem(x.suc, n)
                f(x.suc)
            } else {
                r.suc < n
                x.suc = q * n + r.suc
                division_theorem(x.suc, n)
                f(x.suc)
            }
            f(x.suc)
        }
    }
    division_theorem(m, n)
}

attributes Nat {
    /// True if this number is composite (has nontrivial factors).
    define is_composite(self) -> Bool {
        exists(b: Nat, c: Nat) {
            1 < b and 1 < c and self = b * c
        }
    }

    /// True if this number is prime (greater than 1 and not composite).
    define is_prime(self) -> Bool {
        1 < self and not self.is_composite
    }

    /// True if this number divides b (equivalently, there exists c such that this * c = b).
    define divides(self, b: Nat) -> Bool {
        exists(c: Nat) { self * c = b }
    }

    /// The factorial of this number (the product 1 * 2 * ... * n).
    define factorial(self) -> Nat {
        match self {
            Nat.0 {
                1
            }
            Nat.suc(pred) {
                self * pred.factorial
            }
        }
    }
}


theorem mul_to_zero(a: Nat, b: Nat) { a * b = 0 implies a = 0 or b = 0 } by {
    if a != 0 and b != 0 {
        let (pred_a: Nat) satisfy { pred_a.suc = a }
        pred_a.suc * b = 0
        b + pred_a * b = 0
        false
    }
}

theorem divisor_lt(a: Nat, b: Nat, c: Nat) {
    a != 0 and 1 < b and a * b = c implies a < c
} by {
    let (d: Nat) satisfy { 1 + d = b and d != 0 }
    a * (1 + d) = c
    1 + d = d.suc
    a * d.suc = c
    a + a * d = c
    a * d != 0
    a != c
    a <= c
}

theorem divides_self(a: Nat) { a.divides(a) }

define true_below(f: Nat -> Bool, n: Nat) -> Bool {
    forall(x: Nat) { x < n implies f(x) }
}

theorem strong_induction(f: Nat -> Bool) {
    forall(k: Nat) {
        true_below(f, k) implies f(k)
    } implies forall(n: Nat) { f(n) }
} by {
    define g(x: Nat) -> Bool {
        true_below(f, x)
    }
    true_below(f, 0)
    g(0)
    forall(x: Nat) {
        if g(x) {
            true_below(f, x)
            f(x)
            forall(y: Nat) {
                if y < x.suc {
                    if y < x {
                        f(y)
                    }
                    if not (y < x) {
                        y = x
                        f(y)
                    }
                    f(y)
                }
            }
            true_below(f, x.suc)
            g(x.suc)
        }
    }
    forall(x: Nat) {
        g(x)
    }
    forall(n: Nat) {
        g(n.suc)
        true_below(f, n.suc)
        f(n)
    }
}

theorem divides_trans(a: Nat, b: Nat, c: Nat) {
    a.divides(b) and b.divides(c) implies a.divides(c)
} by {
    let (d: Nat) satisfy { a * d = b }
    let (e: Nat) satisfy { b * e = c }
    a * (d * e) = c
}

theorem has_prime_divisor(n: Nat) {
    1 < n implies exists(p: Nat) {
        p.is_prime and p.divides(n)
    }
} by {
    forall(k: Nat) {
        if true_below(has_prime_divisor, k) {
            if k.is_prime {
                k.divides(k)
                has_prime_divisor(k)
            }
            if k.is_composite {
                let (b: Nat, c: Nat) satisfy {
                    1 < b and 1 < c and k = b * c
                }
                b != 0
                1 < c
                b * c = k
                b < k
                has_prime_divisor(b)
                let (p: Nat) satisfy {
                    p.is_prime and p.divides(b)
                }
                p.divides(k)
                has_prime_divisor(k)
            }
            has_prime_divisor(k)
        }
    }
    has_prime_divisor(n)
}

theorem factorial_zero {
    0.factorial = 1
}

theorem factorial_one {
    1.factorial = 1
}

theorem factorial_step(n: Nat) {
    n.suc.factorial = n.suc * n.factorial
}

theorem divides_factorial(k: Nat, n: Nat) {
    k != 0 and k <= n implies k.divides(n.factorial)
} by {
    define f(x: Nat) -> Bool {
        divides_factorial(k, x)
    }
    not k <= 0
    divides_factorial(k, 0)
    f(0)
    forall(x: Nat) {
        if f(x) {
            divides_factorial(k, x)
            k <= x implies k.divides(x.factorial)
            if k <= x.suc {
                if k = x.suc {
                    x.suc.divides(x.suc.factorial)
                    k.divides(x.suc.factorial)
                } else {
                    k.suc <= x.suc
                    k <= x
                    k.divides(x.factorial)
                    x.factorial.divides(x.suc * x.factorial)
                    x.factorial.divides(x.suc.factorial)
                    k.divides(x.suc.factorial)
                }
                divides_factorial(k, x.suc)
            }
            divides_factorial(k, x.suc)
            f(x.suc)
        }
    }
    divides_factorial(k, n)
}

theorem factorial_nondecreasing(n: Nat) { n.factorial <= n.suc.factorial } by {
    n.factorial <= n.suc * n.factorial
}

theorem lte_one_factorial(a: Nat) { 1 <= a.factorial } by {
    1 <= 0.factorial
    lte_one_factorial(0)
    forall(x: Nat) {
        if lte_one_factorial(x) {
            1 <= x.factorial
            x.factorial <= x.suc.factorial
            1 <= x.suc.factorial
            lte_one_factorial(x.suc)
        }
    }
}

theorem lt_imp_lt_suc(a: Nat, b: Nat) { a < b implies a < b.suc } by {
    let (c: Nat) satisfy { a + c = b and c != 0 }
    a + c.suc = b.suc
    c.suc != 0
}

theorem lte_mul_both(a: Nat, b: Nat, c: Nat) { b <= c implies a * b <= a * c } by {
    let (d: Nat) satisfy { b + d = c }
    a * (b + d) = a * c
    a * b + a * d = a * c
    a * b <= a * c
}

theorem lt_mul_both(a: Nat, b: Nat, c: Nat) { a != 0 and b < c implies a * b < a * c } by {
    let (d: Nat) satisfy { b + d = c }
    c != b
    d != 0
    a * (b + d) = a * c
    a * b + a * d = a * c
    a * d != 0
    if a * b = a * c {
        a * b + a * d = a * b
        let y: Nat = a * b
        let z: Nat = a * d
        y + z = y
        false
    }
    a * b <= a * c
    a * b < a * c
}

theorem lt_cancel_mul(a: Nat, b: Nat, c: Nat) { a != 0 and a * b < a * c implies b < c } by {
    if c <= b {
        a * c <= a * b
        false
    }
    c != b
}

theorem mul_to_one(a: Nat, b: Nat) { a * b = 1 implies a = 1 } by {
    1 != 0
    a != 0
    not a < 0.suc
    not (a < 1)
    b != 0
    if 1 < a {
        b * 1 < b * a
        b * 1 < a * b
        b * 1 < 1
        b * 1 = 0
        b = 0
        false
    }
}

theorem divides_suc(a: Nat, b: Nat) { a.divides(b) and a.divides(b.suc) implies a = 1 } by {
    let (k0: Nat) satisfy { a * k0 = b.suc }
    a != 0
    let (c: Nat) satisfy { a * c = b }
    let (d: Nat) satisfy { a * d = b.suc }
    a * c < a * d
    c <= d
    let (e: Nat) satisfy { c + e = d }
    a * (c + e) = b.suc
    a * c + a * e = b.suc
    b + a * e = b.suc
    b + a * e = b + 1
    a * e = 1
}

theorem exists_infinite_primes(n: Nat) {
    exists(p: Nat) {
        n < p and p.is_prime
    }
} by {
    let m: Nat = n.factorial.suc
    if not (1 < n.factorial.suc) {
        not (1 < n.factorial)
        n.factorial = 1
        false
    }
    1 < m
    let (p: Nat) satisfy {
        p.is_prime and p.divides(m)
    }
    p != 1
    if p <= n {
        divides_factorial(p, n)
        p.divides(n.factorial)
        p.divides(n.factorial.suc)
        false
    }
    not (p <= n)
    n < p
}

theorem divides_zero(a: Nat) {
    a.divides(0)
} by {
    exists(k0: Nat) { a * k0 = 0 }
}

theorem zero_divides(a: Nat) { 0.divides(a) implies a = 0 }

theorem divides_mul(a: Nat, b: Nat, d: Nat) { d.divides(a) implies d.divides(a * b) } by {
    let (q: Nat) satisfy { q * d = a }
    q * d * b = a * b
    d * q * b = a * b
    d * (q * b) = a * b
}

theorem lte_mul(a: Nat, b: Nat) { b != 0 implies a <= a * b } by {
    not b < 0.suc
    not (b < 1)
    1 <= b
    a * 1 <= a * b
}

theorem divides_lte(a: Nat, b: Nat) { a.divides(b) implies b = 0 or a <= b } by {
    if b != 0 {
        let (q: Nat) satisfy { q * a = b }
        if q = 0 {
            b = 0
            false
        }
        a <= a * q
        a <= q * a
        a <= b
    }
}

theorem divides_add(a: Nat, b: Nat, d: Nat) {
    d.divides(a) and d.divides(b) implies d.divides(a + b)
} by {
    let (qa: Nat) satisfy { qa * d = a }
    let (qb: Nat) satisfy { qb * d = b }
    (qa + qb) * d = qa * d + b
    (qa + qb) * d = a + b
    d.divides((qa + qb) * d)
}


theorem divides_symm(a: Nat, b: Nat) { a.divides(b) and b.divides(a) implies a = b } by {
    if a = 0 {
        0.divides(b)
        b = 0
        a = b
    } else {
        b != 0
        a <= b
        b <= a
        if a != b {
            a < b
            b < a
            false
        }
        a = b
    }
}

theorem cross_sum_lte(a: Nat, b: Nat, c: Nat, d: Nat) { a + b = c + d and a <= c implies d <= b } by {
    let (e: Nat) satisfy { a + e = c }
    a + b = a + e + d
    a + b = a + (e + d)
    b = e + d
    d <= b
}

theorem sum_lte(a: Nat, b: Nat, c: Nat, d: Nat) { a <= c and b <= d implies a + b <= c + d } by {
    let (e: Nat) satisfy { a + e = c }
    let (f: Nat) satisfy { b + f = d }
    a + b + (e + f) = c + d
}

// This is a "bounded" version of subtraction that returns 0 instead of negative numbers.
let bounded_sub(a: Nat, b: Nat) -> d: Nat satisfy {
    if a < b {
        d = 0
    } else {
        d + b = a
    }
} by {
    if a < b {
        0 = 0
    } else {
        b <= a
        let d: Nat satisfy { d + b = a }
    }
}

attributes Nat {
    /// Subtraction on natural numbers is defined oddly; it "caps out" at zero.
    /// If `self < b`, then `self - b = 0`.
    /// It would be better to define this as "not valid" on some inputs, but
    /// the language doesn't make that convenient yet.
    define sub(self, b: Nat) -> Nat { bounded_sub(self, b) }
}

theorem sub_lt(a: Nat, b: Nat) { a < b implies a - b = 0 }

theorem add_sub(a: Nat, b: Nat) {
    b <= a implies a - b + b = a
} by {
    not a < b
}

theorem sub_add(a: Nat, b: Nat) { (a + b) - b = a } by {
    b <= a + b
    ((a + b) - b) + b = a + b
}

theorem sub_self(a: Nat) { a - a = 0 }

theorem sub_zero(a: Nat) { a - 0 = a }

theorem add_imp_sub(a: Nat, b: Nat, c: Nat) { a + b = c implies c - b = a }

let mod(a: Nat, m: Nat) -> r: Nat satisfy {
    if m != 0 {
        r < m and exists(q: Nat) { q * m + r = a }
    } else {
        // It doesn't really matter how we define "mod 0".
        // We pick a mod 0 = a.
        r = a
    }
} by {
    if m != 0 {
        0 < m
        let (q: Nat, r: Nat) satisfy { r < m and q * m + r = a }
    } else {
        a = a
    }
}

attributes Nat {
    /// The remainder when dividing this number by m.
    let mod = mod
}

theorem add_mod(a: Nat, m: Nat) {
    exists(q: Nat) { q * m + a.mod(m) = a }
} by {
    if m = 0 {
        a.mod(0) = a
        0 * 0 = 0
        0 * 0 + a.mod(0) = a
        let (q: Nat) satisfy { q * m + a.mod(m) = a }
    } else {
        let (q: Nat) satisfy { q * m + a.mod(m) = a }
    }
}

theorem mod_by_zero(a: Nat) { a.mod(0) = a }

theorem mod_of_zero(m: Nat) {
    0.mod(m) = 0
} by {
    if m = 0 {
        0.mod(m) = 0
    } else {
        0.mod(m) = 0
    }
}

theorem mod_lte(a: Nat, m: Nat) {
    a.mod(m) <= a
} by {
    if m = 0 {
        a.mod(m) <= a
    } else {
        let q: Nat satisfy { q * m + a.mod(m) = a }
        a.mod(m) + q * m = a
        a.mod(m) <= a
    }
}

theorem div_sub_mod(a: Nat, m: Nat) { m.divides(a - a.mod(m)) } by {
    let (q: Nat) satisfy { q * m + a.mod(m) = a }
    a - a.mod(m) = q * m
    m.divides(q * m)
}

theorem sub_left_distrib(a: Nat, b: Nat, c: Nat) {
    a * (b - c) = a * b - a * c
} by {
    if a = 0 {
        a * (b - c) = a * b - a * c
        sub_left_distrib(a, b, c)
    } else {
        if b < c {
            a * b < a * c
            a * b - a * c = 0
            b - c = 0
            a * (b - c) = a * b - a * c
            sub_left_distrib(a, b, c)
        }
        if not (b < c) {
            b - c + c = b
            a * (b - c) + a * c = a * b
            a * (b - c) = a * b - a * c
            sub_left_distrib(a, b, c)
        }
        sub_left_distrib(a, b, c)
    }
    sub_left_distrib(a, b, c)
}

theorem sub_right_distrib(a: Nat, b: Nat, c: Nat) {
    (a - b) * c = a * c - b * c
} by {
    (a - b) * c = c * a - c * b
    (a - b) * c = a * c - c * b
}

theorem divides_sub(a: Nat, b: Nat, d: Nat) {
    d.divides(a) and d.divides(b) implies d.divides(a - b)
} by {
    let qa: Nat satisfy { qa * d = a }
    let qb: Nat satisfy { qb * d = b }
    a - b = qa * d - qb * d
    a - b = (qa - qb) * d
    d.divides((qa - qb) * d)
    d.divides(a - b)
}

theorem divides_mod(a: Nat, m: Nat, d: Nat) {
    d.divides(a) and d.divides(m) implies d.divides(a.mod(m))
} by {
    let (q: Nat) satisfy { q * m + a.mod(m) = a }
    a.mod(m) + q * m = a
    a.mod(m) = a - q * m
    d.divides(q * m)
    d.divides(a - q * m)
    d.divides(a.mod(m))
}

theorem div_imp_mod(a: Nat, m: Nat) { m.divides(a) implies a.mod(m) = 0 } by {
    if m != 0 {
        m.divides(m)
        m.divides(a.mod(m))
        a.mod(m) < m
        not (m <= a.mod(m))
        a.mod(m) = 0
    } else {
        a = 0
        a.mod(m) = 0
    }
    a.mod(m) = 0
}

theorem small_mod(a: Nat, m: Nat) { a < m implies a.mod(m) = a } by {
    let (q: Nat) satisfy { q * m + a.mod(m) = a }
    if q = 0 {
        0 + a.mod(m) = a
        a.mod(m) = a
    } else {
        m != 0
        q * m != 0
        m.divides(q * m)
        m <= q * m
        m <= a
        false
    }
    a.mod(m) = a
}

theorem mod_mod(a: Nat, m: Nat) { a.mod(m).mod(m) = a.mod(m) } by {
    if m != 0 {
        a.mod(m) < m
        a.mod(m).mod(m) = a.mod(m)
        mod_mod(a, m)
    } else {
        a.mod(m).mod(m) = a.mod(m)
        mod_mod(a, m)
    }
}

theorem mod_mul(m: Nat, q: Nat) { (q * m).mod(m) = 0 } by {
    m.divides(q * m)
}

theorem divides_add_copy(a: Nat, b: Nat, d: Nat) {
    d.divides(a) and d.divides(b) implies d.divides(a + b)
}

theorem divides_unmod(d: Nat, a: Nat, m: Nat) {
    d.divides(m) and d.divides(a.mod(m)) implies d.divides(a)
} by {
    let (q: Nat) satisfy { q * m + a.mod(m) = a }
    m.divides(q * m)
    d.divides(q * m)
    d.divides(q * m + a.mod(m))
    d.divides(a)
    if not divides_unmod(d, a, m) {
        d.divides(m)
        d.divides(a.mod(m))
        false
    }
}

theorem mul_cancel_left(a: Nat, b: Nat, c: Nat) {
    a != 0 and a * b = a * c implies b = c
} by {
    not a * c < a * b
    not a * b < a * c
    not b < c
}

theorem mul_cancel_right(a: Nat, b: Nat, c: Nat) {
    a != 0 and b * a = c * a implies b = c
}

theorem divides_cancel_left(a: Nat, b: Nat, c: Nat) {
    a != 0 and (a * b).divides(a * c) implies b.divides(c)
} by {
    let d: Nat satisfy {
        a * b * d = a * c
    }
    a * (b * d) = a * c
    b * d = c
}

theorem divides_cancel_right(a: Nat, b: Nat, c: Nat) {
    a != 0 and (b * a).divides(c * a) implies b.divides(c)
}

theorem divides_mul_left(a: Nat, b: Nat, m: Nat) {
    a.divides(b) implies (m * a).divides(m * b)
} by {
    let d: Nat satisfy {
        a * d = b
    }
    m * a * d = m * b
}

theorem divides_mul_right(a: Nat, b: Nat, m: Nat) {
    a.divides(b) implies (a * m).divides(b * m)
}

// Misc helpers
theorem two_neq_zero {
    2 != 0
}
theorem three_neq_zero {
    3 != 0
}

attributes Nat {
    /// Note that 0^0 = 1.
    /// TODO: ideally this would use an inherited `pow` from `Monoid`.
    define exp(self, b: Nat) -> Nat {
        match b {
            Nat.0 {
                1
            }
            Nat.suc(pred) {
                self * self.exp(pred)
            }
        }
    }
}

theorem exp_one(a: Nat) {
    a.exp(1) = a
} by {
    a * a.exp(0) = a
    a.exp(0.suc) = a
}

theorem exp_zero(a: Nat) {
    a.exp(0) = 1
}

theorem exp_add(a: Nat, b: Nat, c: Nat) {
    a.exp(b + c) = a.exp(b) * a.exp(c)
} by {
    // Inductive step
    let f: Nat -> Bool = function(x: Nat) {
        a.exp(b + x) = a.exp(b) * a.exp(x)
    }
    f(0)
    forall(x: Nat) {
        if f(x) {
            a.exp(b + x) = a.exp(b) * a.exp(x)
            a.exp(b + x.suc) = a.exp(b) * a.exp(x.suc)
            f(x.suc)
        }
    }
    f(c)
}

theorem exp_mul(a: Nat, b: Nat, c: Nat) {
    a.exp(b * c) = a.exp(b).exp(c)
} by {
    // Inductive step
    let f: Nat -> Bool = function(x: Nat) {
        a.exp(b * x) = a.exp(b).exp(x)
    }
    f(0)
    forall(x: Nat) {
        if f(x) {
            a.exp(b * x) = a.exp(b).exp(x)
            a.exp(b * x.suc) = a.exp(b).exp(x.suc)
            f(x.suc)
        }
    }
    f(c)
}

theorem zero_exp(n: Nat) {
    n != 0 implies 0.exp(n) = 0
} by {
    let b: Nat satisfy {
        n = b.suc
    }
}

theorem one_exp(n: Nat) {
    1.exp(n) = 1
} by {
    one_exp(0)
    one_exp(n)
}

theorem exp_gte_one(a: Nat, b: Nat) {
    a != 0 implies 1 <= a.exp(b)
} by {
    // Induction step
    let f: Nat -> Bool = function(x: Nat) {
        1 <= a.exp(x)
    }
    f(0)
    forall(x: Nat) {
        if f(x) {
            1 <= a.exp(x)
            a.exp(x) <= a.exp(x.suc)
            1 <= a.exp(x.suc)
            f(x.suc)
        }
    }
    f(b)
}

theorem exp_gt_one(a: Nat, b: Nat) {
    1 < a and b != 0 implies 1 < a.exp(b)
} by {
    let b_pred: Nat satisfy {
        b = b_pred.suc
    }
    a.exp(b) = a * a.exp(b_pred)
    1 <= a.exp(b_pred)
}

theorem exp_ne_zero(a: Nat, b: Nat) {
    a != 0 implies a.exp(b) != 0
} by {
    1 <= a.exp(b)
}

theorem lte_imp_exp_lte(a: Nat, b: Nat, c: Nat) {
    a != 0 and b <= c implies a.exp(b) <= a.exp(c)
} by {
    let d: Nat satisfy {
        b + d = c
    }
    a.exp(c) = a.exp(b) * a.exp(d)
    a.exp(d) != 0
    a.exp(b) <= a.exp(c)
}

theorem lte_exp(a: Nat, b: Nat) {
    a != 0 and b != 0 implies a <= a.exp(b)
}

theorem lt_imp_exp_lt(a: Nat, b: Nat, c: Nat) {
    1 < a and b < c implies a.exp(b) < a.exp(c)
} by {
    let d: Nat satisfy {
        b + d = c
    }
    d != 0
    a.exp(c) = a.exp(b) * a.exp(d)
    a.exp(b) != 0
    1 < a.exp(d)
}

theorem exp_lte_imp_lte(a: Nat, b: Nat, c: Nat) {
    1 < a and a.exp(b) <= a.exp(c)
    implies
    b <= c
}

theorem exp_lt_imp_lt(a: Nat, b: Nat, c: Nat) {
    1 < a and a.exp(b) < a.exp(c)
    implies
    b < c
}

theorem exp_eq_one_imp(a: Nat, b: Nat) {
    a != 1 and a.exp(b) = 1 implies b = 0
} by {
    if b != 0 {
        let b_pred: Nat satisfy {
            b = b_pred.suc
        }
        a.exp(b) = a * a.exp(b_pred)
        false
    }
}

theorem sq_eq_mul(a: Nat) {
    a.exp(2) = a * a
}

theorem gte_each_of_three(a: Nat, b: Nat, c: Nat) {
    exists(n: Nat) {
        a <= n and b <= n and c <= n
    }
} by {
    let n1: Nat satisfy {
        a <= n1 and b <= n1
    }
    let n2: Nat satisfy {
        n1 <= n2 and c <= n2
    }
    a <= n2 and b <= n2 and c <= n2
}

theorem gte_each_of_three_regular(a: Nat, b: Nat, c: Nat) {
    exists(n: Nat) {
        n >= a and n >= b and n >= c
    }
} by {
    let n: Nat satisfy {
        a <= n and b <= n and c <= n
    }
}

theorem gt_each_of_three_swapped(a: Nat, b: Nat, c: Nat) {
    exists(n: Nat) {
        a < n and b < n and c < n
    }
} by {
    let n1: Nat satisfy {
        a <= n1 and b <= n1 and c <= n1
    }
    let n: Nat satisfy {
        n1 < n
    }
    a < n
    b < n
    c < n
}

theorem gt_each_of_three_regular(a: Nat, b: Nat, c: Nat) {
    exists(n: Nat) {
        n > a and n > b and n > c
    }
} by {
    let n: Nat satisfy {
        a < n and b < n and c < n
    }
}

// Single-digit addition facts should probably just be theorems.
theorem one_plus_two {
    1 + 2 = 3
}

theorem one_plus_three {
    1 + 3 = 4
}

theorem one_plus_four {
    1 + 4 = 5
}

theorem one_plus_five {
    1 + 5 = 6
}

instance Nat: LinearOrder
